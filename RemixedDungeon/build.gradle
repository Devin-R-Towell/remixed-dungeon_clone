apply plugin: 'com.android.application'

ext.getApiField = { name ->
    def stringsFile = "RemixedDungeon/src/main/res/values/strings_api_signature.xml"
    return new XmlParser().parse(stringsFile).string.find { it.@name.equals name }.text()
}

def jks_keyAlias    = System.getenv('jks_keyAlias') ? System.getenv('jks_keyAlias') : 'testKey'
def jks_keyPassword = System.getenv('jks_keyPassword') ? System.getenv('jks_keyPassword') : '123456'
def jks_storePassword = System.getenv('jks_storePassword') ? System.getenv('jks_storePassword') : '123456'
def jks_storePath = System.getenv('jks_storePath')  ? System.getenv('jks_storePath') : 'testKey/test.jks'

android {

    signingConfigs {
        release {
            keyAlias jks_keyAlias
            keyPassword jks_keyPassword
            storeFile file(jks_storePath)
            storePassword jks_storePassword
        }
    }

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = ["admob_app_id":getApiField("admob_app_id"),
                                "gow_app_id":getApiField("gow_app_id")]

        applicationId "com.nyrds.pixeldungeon.ml"
        versionCode 1099
        versionName "31.0.fix.17"
        targetSdkVersion 31
        minSdkVersion 16
        compileSdkVersion = 31

        // Enabling multidex support.
        multiDexEnabled true
    }


    buildTypes {
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            minifyEnabled false
        }
        debug {
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility '1.8'
        targetCompatibility '1.8'
    }

    packagingOptions {
        exclude "META-INF/MANIFEST.MF"
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions "platform","market"

    productFlavors {
        googlePlay {
            dimension "market"
            versionCode defaultConfig.versionCode + 10000
        }

        none {
            dimension "market"
        }

        android {
            dimension "platform"
        }

        libgdx {
            dimension "platform"
            packagingOptions {
                exclude "META-INF/INDEX.LIST"
            }
        }

        none {
            dimension "market"
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/android/AndroidManifest.xml'
            //java.srcDirs = ['src/android/java']
            //res.srcDirs = ['src/android/res'] # need to sync transifex first
        }
    }
    androidResources {
        noCompress 'mp3', 'ogg'
    }
}

tasks.whenTaskAdded {task ->
    if(task.name.contains("generate") && task.name.contains("ResValue")) {
        task.enabled = false
    }
}

dependencies {
    implementation project(':annotation')
    implementation project(path: ':json_clone')

    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.luaj:luaj-jse:3.0.1'
    implementation 'info.guardianproject.netcipher:netcipher:1.2'

    annotationProcessor project(':processor')

    libgdxImplementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    libgdxImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    libgdxImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    libgdxImplementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"


    androidImplementation 'com.google.android.ads.consent:consent-library:1.0.8'


    androidImplementation 'androidx.multidex:multidex:2.0.1'

    implementation 'commons-io:commons-io:2.6'

    androidImplementation 'com.appodeal.ads.sdk:core:2.11.0'
    androidImplementation 'com.appodeal.ads.sdk.networks:adcolony:2.11.0.18'
    androidImplementation 'com.appodeal.ads.sdk.networks:applovin:2.11.0.20'
    androidImplementation 'com.appodeal.ads.sdk.networks:bidmachine:2.11.0.17'
    androidImplementation 'com.appodeal.ads.sdk.networks:ironsource:2.11.0.18'
    androidImplementation 'com.appodeal.ads.sdk.networks:unity_ads:2.11.0.15'
    androidImplementation 'com.appodeal.ads.sdk.networks:vungle:2.11.0.11'

    implementation 'org.jetbrains:annotations:23.0.0'

    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    compileOnly 'org.projectlombok:lombok:1.18.22'

    googlePlayImplementation 'com.google.android.gms:play-services-base:18.0.1'
    googlePlayImplementation 'com.google.android.gms:play-services-auth:20.0.1'
    googlePlayImplementation "com.google.android.gms:play-services-games:22.0.1"
    googlePlayImplementation 'com.google.android.gms:play-services-ads:20.5.0'


    googlePlayImplementation 'com.android.billingclient:billing:4.0.0'

    googlePlayImplementation platform('com.google.firebase:firebase-bom:28.4.2')


    googlePlayImplementation 'com.google.firebase:firebase-analytics'
    googlePlayImplementation 'com.google.firebase:firebase-crashlytics'
    googlePlayImplementation 'com.google.firebase:firebase-perf'
}

if (getGradle().getStartParameter().getTaskRequests()
        .toString().contains("GooglePlay")) {
    apply plugin: 'com.google.firebase.crashlytics'
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.firebase-perf'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}